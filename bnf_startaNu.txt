Tommy Lindman - tomli962
Emil Nilsson - emini901

BNF-GRAMMAR

<program> 	::= startaNu <nyrad> <satser> slutaNu
          	| <satser>

<satser>  	::= <sats>
          	| <satser> <sats>

<sats>    	::= <loop>
          	| <om>
          	| <skriv>
		| <deklarering>
          	| <funktion>
          	| <funktionsanrop>
	    	| <tilldelning>
          	| <nyrad>

<loop>     	::= <for>
		| <medans>
		| <avbryt_loop>

<skriv>	        ::= skriv <uttry>

<for>		::= för varje <identifierare> i <identifierare> <nyrad> start <nyrad> <satser> slut
		| för varje <identifierare> , <identifierare> i <identifierare> <nyrad> start <nyrad> <satser> slut
		| för <identifierare> <heltal> till <heltal> <nyrad> start <nyrad> <satser> slut
       	        
<medans>	::= medans <logiskt_uttryck> <nyrad> start <nyrad> <satser> slut

<avbryt_loop>	::= avbryt loop

<om> 		::= om <logiskt_uttryck> <nyrad> start <nyrad> <satser> <annars_kropp>* slut

<annars_kropp>  ::= annars <satser>

<deklarering>	::= <lista>
		| skapa <identifierare> [ = <funktionsanrop> | = <uttry> ]?

<tilldelning> 	::= lägg till <aritm_uttryck> <listitem>? <identifierare>
		| lägg till <varde> : <varde> <parlistitem>? <identifierare>
		| <identifierare> =  [ <uttry> | <funktionsanrop> ]
		| <snabbtilldelning>

<snabbtilldelning>	::= <identifierare> [ += | -= | *= | /= ] <uttry>

<funktion> 	::= skapa metoden <identifierare> <parameter_lista_deklarering>? <nyrad> start <satser> slut
		| <funktionsanrop>
		| returnera <utty>

<funktionsanrop> 	::= kör <identifierare> [med <parameter_lista_anrop>]?
			| <builtinfuncs>

<parameter_lista_deklarering>	::= <identifierare>
				| <parameter_lista_deklarering> , <identifierare> 

<parameter_lista_anrop>		::= <varde>
				| <parameter_lista_anrop> , <varde>

<builtinfuncs>	::= <identifierare> [ .längd | .storlek | .spräng | .till_strang | till_heltal | till_flyttal | .klass ]


<lista> 	::= skapa <identifierare> ParLista [ = <parlistitem> ]?
		| skapa <identifierare> Lista [ = <listitem> ]?
		| ta bort <aritm_uttryck> <identifierare>
		| <identifierare> [ <aritm_uttryck> ] = <listitem>

<listitem>	::= <aritm_uttryck>
		| <listitem> , <aritm_uttryck>

<parlistitem>	::= <varde> : <varde>
		| <parlistitem> , <varde> : <varde>


<uttry>  	::= <logiskt_uttryck>
          	| <aritm_uttryck>
          	| <identifierare>

<identifierare> ::= /[a-zA-Z0-9]+/


<logiskt_uttryck>       ::= <logiskt_uttryck> <logisk_operator> <logiskt_uttryck>
			| <jamforelse> <jamf_operator> <jamforelse>

<jamforelse>	        ::= <aritm_uttryck>

<logisk_operator>       ::= och
			| eller
			| inte är

<jamf_operator>  	::= < | > | >= | <= | == | !=

<nyrad> 		::= NEWLINE
 
<aritm_uttryck> 	::= <aritm_uttryck> + <term>
			| <aritm_uttryck> - <term>
			| <term>

<term> 		        ::= <term> * <faktor>
			| <term> / <faktor>
			| <term> ^ <faktor>
			| <term> % <faktor>
			| <faktor>

<faktor> 		::= ( <aritm_uttryck> )
			| <varde>

<varde>		        ::= <bool>
			| <heltal>
			| <flyttal>
			| <strang>

<bool>		        ::= sant
			| falskt

<heltal>		::= Integer

<flyttal>		::= Float

<strang>		::= /\”.+\”/
			| <identifierare>

